apply plugin: 'com.android.application'

apply plugin: "com.facebook.react"
apply plugin: 'kotlin-android'

def bootstrapVersion = "bootstrap-2025.12.11-r1+apt-android-7"
def bootstrapHashes = parseHashes("""
a0b86567cd9468d2d1f0986add6f12b676739ade0ca2d95c9094b2dc16675b27    bootstrap-aarch64.zip
dfb947a39bd9f003d79e1082660ca9d3364529ab1ed8f104412064cd8df3345f    bootstrap-arm.zip
6a4963f41a07e907c470503bba5952e2463fd3b563674212daf23da17a5098a4    bootstrap-i686.zip
76acfd30f4fd5fee20d1ec856f06c0eff470d20b9dc1388e85c6fc2ed0c40ed3    bootstrap-x86_64.zip
""")
def csVersion = "cs-2025.12.09-r1"
def csHashes = parseHashes("""
0ee4d23609d2e013be28d822ccccefddc1619dcd508a2976f9bd1809d5c0d57c    cs-aarch64.tgz
3cad659b2b5133dfd6bae5ed2c254e90d31c114d4cb30689b37885eca104f2a2    cs-arm.tgz
f48e717cca38060676fcb88b01da534f82d4976ce021a02d96178c516a86e358    cs-x86.tgz
f0a1ffeb86d6e836d4829cc859f70821b6edfd5cf885a90ea553f0535ed1fc7a    cs-x86_64.tgz
""")

def packageJsonFile = new File(projectDir, "../package.json")
def packageJson = new groovy.json.JsonSlurper().parseText(packageJsonFile.text)

react {
    cliFile = file("${rootProject.ext.rootDir}/node_modules/react-native/cli.js")
    root = file("${rootProject.ext.rootDir}")
//    jsRootDir = file("../Libraries")
    reactNativeDir = file("${rootProject.ext.rootDir}/node_modules/react-native")
}

android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    ndkVersion System.getenv("JITPACK_NDK_VERSION") ?: project.properties.ndkVersion

    dexOptions {
        javaMaxHeapSize "4G"
    }

    defaultConfig {
        applicationId "vn.vhn.vsc"
        minSdkVersion project.properties.minSdkVersion.toInteger()
        if (System.env['GPLAY_BUILD'] == '1') {
            buildConfigField "Boolean", "GOOGLEPLAY_BUILD", "true"
            defaultConfig.targetSdkVersion 30 // google play require minimum version to be 30
        } else {
            buildConfigField "Boolean", "GOOGLEPLAY_BUILD", "false"
            defaultConfig.targetSdkVersion 28 // Dont update - /data/data no longer executable
        }
        versionCode packageJson.config.versionCode
        versionName System.getenv("CI_VERSION_NAME") ?: "git describe --tags --abbrev=0".execute().text.trim()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            if (System.env['GPLAY_BUILD'] != '1' && System.env['CI_BUILD'] != '1') {
                abiFilters 'arm64-v8a'
            } else if (System.env['USE_SPLITBUILD'] != '1') {
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
        }

        buildConfigField "String", "BOOTSTRAP_VERSION", "\"${bootstrapVersion}\""
        buildConfigField "String", "CS_VERSION", "\"${csVersion}\""
    }

    if (System.env['USE_SPLITBUILD'] == '1') {
        splits {
            abi {
                enable true
                reset()
                include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                universalApk false
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('keystore')
            keyAlias 'alias'
            storePassword '123456'
            keyPassword '123456'
        }
        release {
            storeFile file('keystore')
            keyAlias 'alias'
            storePassword '123456'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
//        ndkBuild {
//            path "src/main/cpp/Android.mk"
//        }
        cmake {
            path file("src/main/cpp/CMakeLists.txt")
        }
    }
    lint {
        lintConfig file('../lint.xml')
    }
    namespace 'vn.vhn.vhscode'
    buildFeatures {
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    applicationVariants.all { variant ->
        if (variant.name == 'debug') {
            variant.outputs.each { output ->
                output.versionCodeOverride = 999999999
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.core:core-ktx:${project.properties.androidxVersion}"
    implementation "androidx.activity:activity-ktx:${project.properties.androidxVersion}"
    implementation "androidx.fragment:fragment-ktx:${project.properties.androidxVersion}"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.github.Dimezis:BlurView:version-2.0.0'

    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'


    implementation "com.facebook.react:react-android"
    if (hermesEnabled.toBoolean()) {
        implementation "com.facebook.react:hermes-android"
    } else {
        implementation "org.webkit:android-jsc:+"
    }


    api project(':termux-shared')
    api project(':terminal-emulator')
    api project(':terminal-view')
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    implementation 'me.jfenn:crasher:0.0.2'
}

clean {
    doLast {
        def tree = fileTree(new File(projectDir, 'src/main/cpp'))
        tree.include 'bootstrap-*.zip'
        tree.each { it.delete() }
    }
}

def downloadRemoteArchive(String arch, String localUrl, String remoteUrl, String expectedChecksum, String version) {
    def digest = java.security.MessageDigest.getInstance("SHA-256")

    def file = new File(projectDir, localUrl)
    if (file.exists()) {
        def buffer = new byte[8192]
        def input = new FileInputStream(file)
        while (true) {
            def readBytes = input.read(buffer)
            if (readBytes < 0) break
            digest.update(buffer, 0, readBytes)
        }
        def checksum = String.format("%1\$64s", new BigInteger(1, digest.digest()).toString(16)).replace(' ', '0')
        if (checksum == expectedChecksum) {
            return
        } else {
            logger.quiet("Deleting old local file with wrong hash: " + localUrl)
            file.delete()
        }
    }

    logger.quiet("Downloading " + remoteUrl + " ...")

    file.parentFile.mkdirs()
    def out = new BufferedOutputStream(new FileOutputStream(file))

    def connection = new URL(remoteUrl).openConnection()
    connection.setInstanceFollowRedirects(true)
    def digestStream = new java.security.DigestInputStream(connection.inputStream, digest)
    out << digestStream
    out.close()

    def checksum = new BigInteger(1, digest.digest()).toString(16)
    if (checksum != expectedChecksum.replaceAll("^0+", "")) {
        file.delete()
        throw new Exception("Wrong checksum for " + remoteUrl + ": expected: " + expectedChecksum + ", actual: " + checksum)
    }
}

def downloadBootstrap(String arch, String expectedChecksum, String version) {
    downloadRemoteArchive(
            arch,
            "src/main/cpp/bootstrap-" + arch + ".zip",
            "https://github.com/vhqtvn/termux-packages/releases/download/" + version + "/bootstrap-" + arch + ".zip",
            expectedChecksum,
            version
    )
}

def downloadCodeServer(String arch, String expectedChecksum, String version) {
    downloadRemoteArchive(
            arch,
            "src/main/cpp/cs-" + arch + ".tgz",
            "https://github.com/vhqtvn/vscode-android-server/releases/download/" + version + "/cs-" + arch + ".tgz",
            expectedChecksum,
            version
    )
}

def parseHashes(String hashesStr) {
    Map<String, String> m = [:]
    for (String line in hashesStr.trim().split("\n")) {
        String[] parts = line.trim().split(" +")
        m[parts[1].trim()] = parts[0].trim()
    }
    return m
}

task downloadBootstraps() {
    doLast {
        downloadBootstrap("aarch64", bootstrapHashes["bootstrap-aarch64.zip"], bootstrapVersion)
        downloadBootstrap("arm", bootstrapHashes["bootstrap-arm.zip"], bootstrapVersion)
        downloadBootstrap("i686", bootstrapHashes["bootstrap-i686.zip"], bootstrapVersion)
        downloadBootstrap("x86_64", bootstrapHashes["bootstrap-x86_64.zip"], bootstrapVersion)

        downloadCodeServer("aarch64", csHashes["cs-aarch64.tgz"], csVersion)
        downloadCodeServer("arm", csHashes["cs-arm.tgz"], csVersion)
        downloadCodeServer("x86", csHashes["cs-x86.tgz"], csVersion)
        downloadCodeServer("x86_64", csHashes["cs-x86_64.tgz"], csVersion)
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(downloadBootstraps)
    }
}

apply from: file("../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)

